package com.hpe.amce.translation.impl

import groovy.transform.CompileStatic
import groovy.util.logging.Log4j2

import javax.annotation.Nonnull
import javax.annotation.Nullable

/**
 * Logs error generated by decorated translator.
 *
 * Once error is logged, it will be thrown again
 * so it can be caught to be reported as metric or ignored or propagated.
 *
 * The message logged contains just the failed stage.
 * No context or parameters are logged. Those are supposed to be already
 * in exception message. This is often the case
 * with {@link com.hpe.amce.mapping.impl.SingleLineMesasgeFormatter}.
 * Use this decorator instead of {@link com.hpe.amce.translation.impl.ElementErrorLoggerDecorator}
 * if you use {@link com.hpe.amce.mapping.impl.SingleLineMesasgeFormatter}
 * to avoid duplication of logging context/parameters/failed element.
 * The message from exception is not duplicated but is supposed to be logged
 * by logging framework.
 *
 * C - type of translation context.
 */
@Log4j2
@CompileStatic
class BriefElementErrorLoggerDecorator<C> implements AroundElement<C> {

    /**
     * Translator to be decorated.
     */
    @Nonnull
    AroundElement<C> next

    /**
     * Creates instance.
     * @param next Translator to be decorated.
     */
    BriefElementErrorLoggerDecorator(@Nonnull AroundElement<C> next) {
        this.next = next
    }

    @Override
    // Want to catch Groovy assertion violations
    @SuppressWarnings('CatchThrowable')
    List<?> translateElement(@Nonnull String stageName, @Nonnull Closure<List<?>> stageCode,
                             @Nullable Object element, @Nullable C context) {
        try {
            next.translateElement(stageName, stageCode, element, context)
        } catch (Throwable e) {
            log.error("$stageName has failed", e)
            throw e
        }
    }
}
